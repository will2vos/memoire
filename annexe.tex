\chapter{Supplementary material for the article}     % numérotée
\label{chap:supp}                   % étiquette pour renvois (à compléter!)

\pagebreak
\section{Ground beetles and springtails identifications}

\begin{table}[h]
    \centering
    \caption[List of ground beetle species captured during summer 2021 in Portneuf Wildlife Reserve and classification between salamander's competitor and prey groups.]
    {List of ground beetle species captured during summer 2021 in Portneuf Wildlife Reserve}
    \label{tab:carabid}
    \begin{tabular}{llllll} 
        \hline
        Group & Specie & Control & Partial-cut & Clearcut & Total \\ [0.5ex] 
        \hline      
        Competitor          & \textit{Chlaenius sericeus}               & 0 & 0 & 2 & 2 \\  
        $\leq$ \hl{8,3 mm}       & \textit{Harpalus erythropus}              & 0 & 0 & 1 & 1 \\
                            & \textit{Harpalus faunus}                  & 0 & 0 & 1 & 1 \\
                            & \textit{Harpalus rufipes}                 & 0 & 0 & 1 & 1 \\
                            & \textit{Patrobus longicornis}             & 0 & 0 & 1 & 1 \\
                            & \textit{Platynus decentis}                & 1 & 1 & 0 & 2 \\
                            & \textit{Poecilus lucublandus}             & 0 & 0 & 3 & 3 \\
                            & \textit{Pterostichus adstrictus}          & 3 & 4 & 20 & 27 \\
                            & \textit{Pterostichus coracinus}           & 6 & 8 & 18 & 32 \\
                            & \textit{Pterostichus diligendus}          & 1 & 1 & 1 & 3 \\
                            & \textit{Pterostichus mutus}               & 0 & 1 & 9 & 10 \\
                            & \textit{Pterostichus pensylvanicus}       & 4 & 8 & 6 & 18 \\
                            & \textit{Pterostichus punctatissimus}      & 1 & 0 & 0 & 1 \\
                            & \textit{Pterostichus tristis}             & 11 & 11 & 3 & 25 \\
                            & \textit{Sphaeroderus canadensis}          & 2 & 4 & 0 & 6 \\
                            & \textit{Sphaeroderus nitidicollis}        & 1 & 0 & 0 & 1 \\
                            \hline 
        Prey                & \textit{Agonum affine}                    & 0 & 4 & 4 & 8 \\ 
        $>$ \hl{8.3 mm}          & \textit{Agonum palustre}                  & 0 & 1 & 2 & 3 \\
                            & \textit{Agonum punctiforme}               & 0 & 0 & 2 & 2 \\ 
                            & \textit{Agonum retractum}                 & 1 & 1 & 1 & 3 \\ 
                            & \textit{Bradycellus lugubris}             & 0 & 0 & 1 & 1 \\
                            & \textit{Bradycellus semipubescens}        & 0 & 0 & 1 & 1 \\
                            & \textit{Calathus gregarius}               & 0 & 0 & 1 & 1 \\
                            & \textit{Harpalus providens}               & 0 & 0 & 1 & 1 \\
                            & \textit{Loricera pilicornis}              & 0 & 0 & 1 & 1 \\
                            & \textit{Notiobia terminata}               & 0 & 1 & 4 & 5 \\
                            & \textit{Pseudamara arenaria}              & 0 & 0 & 7 & 7 \\
                            & \textit{Pterostichus commutabilis}        & 0 & 0 & 1 & 1 \\
                            & \textit{Synuchus impunctatus}             & 4 & 3 & 13 & 20 \\
                            & \textit{Trechus apicalis}                 & 0 & 1 & 0 & 1 \\
                            \hline 
        \textbf{Total}      &                                           & 35 & 49 & 105 & 189 \\
        \hline
    \end{tabular}
  \end{table}

  \begin{table}[ht]
    \centering
    \caption[Counts of springtails at the family level, per harvest treatments]
    {Counts of springtails at the family level captured during summer 2021 in Portneuf Wildlife Reserve, per harvest treatments.}
    \label{tab:springtail}
    \begin{tabular}{llllll} 
        \hline
        Order & Family & Control & Partial-cut & Clearcut & Total \\ [0.5ex] 
        \hline      
        Entomobryomorpha    & Entomobryidae     & 10 & 39 & 13 & 62 \\  
                            & Isotomidae        & 62 & 88 & 32 & 182 \\
                            & Tomoceridae       & 0 & 5 & 3 & 8 \\
        Poduromorpha        & Hypogastruridae   & 21 & 29 & 42 & 92 \\
                            & Neanuridae        & 1 & 9 & 1 & 11 \\
                            & Onychiuridae      & 6 & 19 & 8 & 33 \\
                            & Tullbergidae      & 4 & 8 & 12 & 24 \\
        Symphypleona        & Dicyrtomidae      & 0 & 3 & 3 & 6 \\
                            & Katiannidae       & 1 & 2 & 3 & 6 \\
                            & Neelidae          & 9 & 8 & 6 & 23 \\
                            & Sminthuridae      & 0 & 5 & 5 & 10 \\
                            & Sminthurididae    & 4 & 4 & 3 & 11 \\
                            \hline 
        \textbf{Total}      &                   & 118 & 219 & 131 & 468 \\
        \hline
    \end{tabular}
  \end{table}

  \clearpage

\section{Structural equation models}


\begin{table}[h!]
\caption[Specification of the linear mixed model components used to estimate impact of overstory treatments on environmental variables that could affect soil fauna habitat selection.]
{Specification of the linear mixed model components used to estimate impact of overstory treatments on environmental variables that could affect soil fauna habitat selection in Portneuf Wildlife Reserve, Québec, Canada.}
\label{ann:SEM_Env_eq}
\end{table}

We used linear mixed models to assess how overstory treatments affect springtail biomass ($\text{Springtail}_{i}$) and 
environmental variables ($\text{CWD}_{i}$, $\text{Canopy}_{i}$, $\text{Litter}_{i}$) at site $i$ :

\begin{align}
  \text{Springtail}_{i} &\sim \text{N} (\mu_{\text{Springtail}_i}, \sigma_{\text{Springtail}_{\text{Group}_i}}) \nonumber \\
  \text{CWD}_{i} &\sim \text{N} (\mu_{\text{CWD}_i}, \sigma_{\text{CWD}_{\text{Group}_i}}) \\
  \text{Canopy}_{i} &\sim \text{N} (\mu_{\text{Canopy}_i}, \sigma_{\text{Canopy}_{\text{Group}_i}}) \nonumber \\
  \text{Litter}_{i} &\sim \text{N} (\mu_{\text{Litter}_i}, \sigma_{\text{Litter}_{i}}) \nonumber 
\end{align}

Due to heteroscedasticity in the springtail biomass, we allowed each treatment group $j$ to have their own variances ($\sigma_j \sim \text{U}(0,150)$). 
Springtail biomass was drawn from a normal distribution $\text{N} (\mu_{\text{Springtail}_i}, \sigma_{\text{Springtail}_{\text{Group}_i}})$, where $\sigma_{\text{Springtail}_{\text{Group}_i}}$ denotes the 
residual variance of a given treatment group and ($\mu_{i}$) corresponds to the linear predictor including overstory treatments ($\text{Cutpartial}_i$, $\text{Cutclear}_i$), 
a block random effect ($\alpha_{\text{Block}}$), as well as the latent occupancy state of salamanders and of each ground beetle group 
($z_{Salamander}$, $z_{Large.carabids}$, $z_{Small.carabids}$):


\begin{align}
  \mu_{\text{Springtail}_i} &= \beta_{0[\text{Springtail}]} + \beta_{\text{Cutpartial}[\text{Springtail}]} \times \text{Cutpartial}_i + \nonumber\\
  &\beta_{\text{Cutclear}[Springtail]} \times \text{Cutclear}_i + \beta_{z_{\text{Salamander}}[\text{Springtail}]} \times z_{Salamander} +  \nonumber\\
  &\beta_{z_{\text{Small.carabids}}[\text{Springtail}]} \times z_{Small.carabids} + \beta_{z_{\text{Large.carabids}}[\text{Springtail}]} \times z_{Large.carabids} + \nonumber\\
  &\alpha_{\text{Block}[\text{Springtail}]_{\text{Block}_i}} \nonumber
\end{align}


Again, we assumed vague normal priors for the coefficients $\text{N}(0, \sigma = \sqrt{10})$. 
We used $\text{N}(0, \alpha_{Block})$ priors for the block random effects, where $\alpha_{Block})$ is drawn from a uniform distribution $\text{U}(0, 50)$. 


\begin{align}
  \mu_{\text{CWD}_i} &= \beta_{0[\text{CWD}]} + \beta_{\text{Cutpartial}[\text{CWD}]} \times \text{Cutpartial}_{i} + \nonumber\\
  & \beta_{\text{Cutclear}[\text{CWD}]} \times \text{Cutclear}_{i} + \alpha_{\text{Block}[\text{CWD}]_{\text{block}_i}} 
\end{align}

\begin{align}
  \mu_{\text{Canopy}_i} &= \beta_{0[\text{Canopy}]} + \beta_{\text{Cutpartial}[\text{Canopy}]} \times \text{Cutpartial}_{i} + \nonumber \\
  & \beta_{\text{Cutclear}[\text{Canopy}]} \times \text{Cutclear}_{i} + \alpha_{\text{Block}[\text{Canopy}]_{\text{block}_i}} \nonumber
\end{align}

\begin{align}
  \mu_{\text{Litter}_i} &= \beta_{0[\text{Litter}]} + \beta_{\text{Cutpartial}[\text{Litter}]} \times \text{Cutpartial}_{i} + \nonumber\\
  & \beta_{\text{Cutclear}[\text{Litter}]} \times \text{Cutclear}_{i} + \alpha_{\text{Block}[\text{Litter}]_{\text{block}_i}} \nonumber
\end{align}

\vspace{10pt}

\begin{table}[h!]
   \caption[Specification of the occupancy model and linear mixed model components used to estimate impact of overstory treatments on red-backed salamander (\textit{Plethodon cinereus}) occupancy, ground beetle occupancy, and springtail biomass.]
   {Specification of the occupancy model and linear mixed model components used to estimate impact of overstory treatments on red-backed salamander (\textit{Plethodon cinereus}) occupancy, ground beetle occupancy, and springtail biomass in Portneuf Wildlife Reserve, Québec, Canada.}
   \label{ann:SEM_Sp_eq}
   \end{table}

The occupancy models of salamanders and ground beetles used the observed detections and non-detections at site $i$ during survey $j$, 
represented by $\text{Salamander}_{ij}$, $\text{Large.carabids}_{ij}$ and $\text{Small.carabids}_{ij}$. 
These data followed a Bernoulli distribution with $z_{i} \times p_{ij}$ as the success parameter, 
where $z_{i}$ represents the latent occupancy state of a given species group at site $i$ and $p_{ij}$ represents the probability of detecting the same species group, given it is present, at site $i$ during survey $j$ :


\begin{align}
  \text{Salamander}_{ij} &\sim \text{Bernoulli}(z_{\text{Salamander}_i} \times p_{\text{Salamander}_{ij}}) \nonumber \\
  \text{Large.carabids}_{ij} &\sim \text{Bernoulli}(z_{\text{Large.carabids}_i} \times p_{\text{Large.carabids}_{ij}})  \\
  \text{Small.carabids}_{ij} &\sim \text{Bernoulli}(z_{\text{Small.carabids}_i} \times p_{\text{Small.carabids}_{ij}}) \nonumber
\end{align}


The latent occupancy state of salamanders and large ground beetles at a site ($z_{i}$) followed a Bernoulli distribution 
with probability of occupancy ($\psi$) of the given species for a given harvest treatment (control, partial cut, clearcut):


\begin{align}
  z_{\text{Salamander}_i} &\sim \text{Bernoulli}(\psi_{\text{Salamander}_{\text{Treat}_i}}) \nonumber \\
  z_{\text{Large.carabids}_i} &\sim \text{Bernoulli}(\psi_{\text{Large.carabids}_{\text{Treat}_i}})
\end{align}


We used uniform distribution priors ($\text{U}(0, 1)$) for the occupancy probabilities of salamanders and large ground beetles in a given treatment. 

For small ground beetles, the latent occupancy state was drawn from a Bernoulli distribution, where the occupancy probability ($\psi_{Small.carabids_{i}}$) 
depended on salamander latent occupancy ($z_{\text{Salamander}_i}$) and overstory treatments (control as reference):


\begin{align}
  \text{logit}(\psi_{\text{Small.carabids}_i}) &= \beta_{0[\text{Small.carabids}]} + \beta_{z_{\text{Salamander}}[\text{Small.carabids}]} \times z_{\text{Salamander}_i} + \nonumber \\
  &\beta_{\text{Cutpartial}[\text{Small.carabids}]} \times \text{Cutpartial}_i + \\
  &\beta_{\text{Cutclear}[\text{Small.carabids}]} \times \text{Cutclear}_i \nonumber
\end{align}

We assumed vague normal priors for the $\beta$ parameters, $\text{N}(0, \sigma = \sqrt{10})$. 
We allowed the detection probability of a given species groups ($\text{logit}(p_{ij})$) with the volume of coarse woody debris ($\text{CWD}_i$) and the precipitation levels 
($\text{Precipitation}_{ij}$) as explanatory variables, and a block random effect ($\alpha_{Block}$) to reflect the experimental design:


\begin{align}
  \text{logit}(p_{\text{Salamander}_{ij}}) &= \alpha_{0[\text{Salamander}]} + \alpha_{\text{CWD}[\text{Salamander}]} \times \text{CWD}_i + \nonumber \\
  &\alpha_{\text{Precipitation}[\text{Salamander}]} \times \text{Precipitation}_{ij} + \alpha_{\text{Block}[\text{Salamander}]_{\text{Block}_i}} \nonumber
\end{align}

\begin{align}
  \text{logit}(p_{\text{Large.carabids}_{ij}}) &= \alpha_{0[\text{Large.carabids}]} + \alpha_{\text{CWD}[\text{Large.carabids}]} \times \text{CWD}_i + \\
  &\alpha_{\text{Precipitation}[\text{Large.carabids}]} \times \text{Precipitation}_{ij} + \alpha_{\text{Block}[\text{Large.carabids}]_{\text{Block}_i}} \nonumber 
\end{align}

\begin{align}
  \text{logit}(p_{\text{Small.carabids}_{ij}}) &= \alpha_{0[\text{Small.carabids}]} + \alpha_{\text{CWD}[\text{Small.carabids}]} \times \text{CWD}_i + \nonumber \\
  &\alpha_{\text{Precipitation}[\text{Small.carabids}]} \times \text{Precipitation}_{ij} + \alpha_{\text{Block}[\text{Small.carabids}]_{\text{Block}_i}} \nonumber 
\end{align}


We used vague normal priors for CWD and precipitation levels $\text{N}(0, \sigma = \sqrt{10})$. 
The priors for block random effects were $\text{N}(0, \alpha_{Block})$, where $\alpha_{Block}$ was drawn from a uniform distribution $\text{U}(0, 10)$. 

\clearpage


\begin{table}[ht]
\caption[JAGS code used to estimate impact of overstory treatments on Red-backed salamanders (\textit{Plethodon cinereus}) and ground beetle occupancy, springtail biomass and environmental variables that could affect soil fauna habitat selection.]
    {JAGS code used to estimate impact of overstory treatments on Red-backed salamanders (\textit{Plethodon cinereus}) and ground beetle occupancy, springtail biomass and environmental variables that could affect soil fauna habitat selection in Portneuf Wildlife Reserve, Québec, Canada.}
    \label{ann:SEM_script}
\end{table}

\begin{lstlisting}
##SEM
model {

##CWD
##priors
beta0.cwd ~ dnorm(0, 0.01)
beta.Cutpartial.cwd ~ dnorm(0, 0.01)
beta.Cutclear.cwd ~ dnorm(0, 0.01)

##block random effect
for(m in 1:nblocks) {
   alpha.block.cwd[m] ~ dnorm(0, tau.block.cwd)
}

##variance of block
tau.block.cwd <- pow(sigma.block.cwd, -2)
sigma.block.cwd ~ dunif(0, 50)

##allow each group to have different variance
for(j in 1:3) {
   tau.cwd[j] <- pow(sigma.cwd[j], -2)
   sigma.cwd[j] ~ dunif(0, 150)
}

##iterate over each observation
for (i in 1:nsites) {

    ##linear predictor  
    mu.cwd[i] <- beta0.cwd + beta.Cutpartial.cwd * Cutpartial[i] + 
       beta.Cutclear.cwd * Cutclear[i] + alpha.block.cwd[Block[i]]

    ##response
    CWD_tot[i] ~ dnorm(mu.cwd[i], tau.cwd[Group[i]])
}

##derived parameters
for(i in 1:nsites) {
    pred.cwd[i] <- mu.cwd[i]
    res.cwd[i] <- CWD[i] - mu.cwd[i]
    res.pearson.cwd[i] <- res.cwd[i]/sigma.cwd[Group[i]]
}

##Canopy openness
##priors
beta0.can ~ dnorm(0, 0.01)
beta.Cutpartial.can ~ dnorm(0, 0.01)
beta.Cutclear.can ~ dnorm(0, 0.01)

##block random effect
for(m in 1:nblocks) {
   alpha.block.can[m] ~ dnorm(0, tau.block.can)
}

##variance of block
tau.block.can <- pow(sigma.block.can, -2)
sigma.block.can ~ dunif(0, 50)

##allow each group to have different variance
for(j in 1:3) {
   tau.can[j] <- pow(sigma.can[j], -2)
   sigma.can[j] ~ dunif(0, 150)
}

##iterate over each observation
for (i in 1:nsites) {

    ##linear predictor  
    mu.can[i] <- beta0.can + beta.Cutpartial.can * Cutpartial[i] + 
       beta.Cutclear.can * Cutclear[i] + alpha.block.can[Block[i]]

    ##response
    Canopy[i] ~ dnorm(mu.can[i], tau.can[Group[i]])
}

##derived parameters
for(i in 1:nsites) {
    pred.can[i] <- mu.can[i]
    res.can[i] <- Canopy[i] - mu.can[i]
    res.pearson.can[i] <- res.can[i]/sigma.can[Group[i]]
}

##Litter depth
##priors
beta0.lit ~ dnorm(0, 0.01)
beta.Cutpartial.lit ~ dnorm(0, 0.01)
beta.Cutclear.lit ~ dnorm(0, 0.01)

##block random effect
for(m in 1:nblocks) {
   alpha.block.lit[m] ~ dnorm(0, tau.block.lit)
}

##variance of block
tau.block.lit <- pow(sigma.block.lit, -2)
sigma.block.lit ~ dunif(0, 50)

##allow each group to have different variance
tau.lit <- pow(sigma.lit, -2)
sigma.lit ~ dunif(0, 150)

##iterate over each observation
for (i in 1:nsites) {

    ##linear predictor  
    mu.lit[i] <- beta0.lit + beta.Cutpartial.lit * Cutpartial[i] + 
       beta.Cutclear.lit * Cutclear[i] + alpha.block.lit[Block[i]]

       ##response
       ##Litter[i] ~ dnorm(mu.lit[i], tau.lit[Group[i]])
       Litter[i] ~ dnorm(mu.lit[i], tau.lit)
}

##derived parameters
for(i in 1:nsites) {
    pred.lit[i] <- mu.lit[i]
    res.lit[i] <- Litter[i] - mu.lit[i]
    res.pearson.lit[i] <- res.lit[i]/sigma.lit
}

##salamander component
##priors for psi
for(k in 1:ngroups) {
   psi.sal[k] ~ dunif(0, 1)
}

##priors for p
alpha0.sal ~ dnorm(0, 0.01)
alpha.precip.sal ~ dnorm(0, 0.01)
alpha.cwd.sal ~ dnorm(0, 0.01)

##block random effect
for(m in 1:nblocks) {
   alpha.block.sal[m] ~ dnorm(0, tau.block.sal)
}

tau.block.sal <- pow(sigma.block.sal, -2)
sigma.block.sal ~ dunif(0, 10)

##salamander model
for(i in 1:nsites) {
   ##occupancy
   z.sal[i] ~ dbern(psi.sal[Group[i]])
   for(j in 1:nvisits) {
      ##p
      logit.p.sal[i, j] <- alpha0.sal + alpha.cwd.sal * CWD[i] + alpha.precip.sal * Precip[i, j] + alpha.block.sal[Block[i]]
      p.sal[i, j] <- exp(logit.p.sal[i, j])/(1 + exp(logit.p.sal[i, j]))

      eff.p.sal[i, j] <- z.sal[i] * p.sal[i, j]
      y.sal[i, j] ~ dbern(eff.p.sal[i, j])
   }
}

finiteOcc.sal <- sum(z.sal[])

####carab beetles - prey
##psi priors
psi.beta0.car.prey ~ dnorm(0, 0.01)
psi.beta.z.sal.car.prey ~ dnorm(0, 0.01)
psi.beta.Cutpartial.car.prey ~ dnorm(0, 0.01)
psi.beta.Cutclear.car.prey ~ dnorm(0, 0.01)

##p priors
alpha0.car.prey ~ dnorm(0, 0.01)
alpha.precip.car.prey ~ dnorm(0, 0.01)
alpha.cwd.car.prey ~ dnorm(0, 0.01)

##block random effects
for(m in 1:nblocks) {
   alpha.block.car.prey[m] ~ dnorm(0, tau.block.car.prey)
}

tau.block.car.prey <- pow(sigma.block.car.prey, -2)
sigma.block.car.prey ~ dunif(0, 10)

for(i in 1:nsites) {
   ##occupancy
   logit(psi.car.prey[i]) <- psi.beta0.car.prey + psi.beta.z.sal.car.prey * z.sal[i] +
    	psi.beta.Cutpartial.car.prey*Cutpartial[i] + psi.beta.Cutclear.car.prey*Cutclear[i]
    	
   z.car.prey[i] ~ dbern(psi.car.prey[i])

   for(j in 1:nvisits) {
      ##p
      logit.p.car.prey[i, j] <- alpha0.car.prey + alpha.cwd.car.prey * CWD[i] + alpha.precip.car.prey * Precip[i, j]
      + alpha.block.car.prey[Block[i]]
      p.car.prey[i, j] <- exp(logit.p.car.prey[i, j])/(1 + exp(logit.p.car.prey[i, j]))

      eff.p.car.prey[i, j] <- z.car.prey[i] * p.car.prey[i, j]
      y.car.prey[i, j] ~ dbern(eff.p.car.prey[i, j])
   }
}

finiteOcc.car.prey <- sum(z.car.prey[])

####carab beetles - comp
##psi priors
for(k in 1:ngroups) {
   psi.car.comp[k] ~ dunif(0, 1)
}

##p priors
alpha0.car.comp ~ dnorm(0, 0.01)
alpha.precip.car.comp ~ dnorm(0, 0.01)
alpha.cwd.car.comp ~ dnorm(0, 0.01)

##block random effects
for(m in 1:nblocks) {
   alpha.block.car.comp[m] ~ dnorm(0, tau.block.car.comp)
}

tau.block.car.comp <- pow(sigma.block.car.comp, -2)
sigma.block.car.comp ~ dunif(0, 10)

for(i in 1:nsites) {
      ##occupancy
      z.car.comp[i] ~ dbern(psi.car.comp[Group[i]])

   	for(j in 1:nvisits) {
      	   ##detection
      	   logit.p.car.comp[i, j] <- alpha0.car.comp + alpha.cwd.car.comp * CWD[i] + alpha.precip.car.comp * Precip[i, j]
           + alpha.block.car.comp[Block[i]]

      	   ##p
      	   p.car.comp[i, j] <- exp(logit.p.car.comp[i, j])/(1 + exp(logit.p.car.comp[i, j]))
      	   eff.p.car.comp[i, j] <- z.car.comp[i] * p.car.comp[i, j]
      	   y.car.comp[i, j] ~ dbern(eff.p.car.comp[i, j])
      	}
}

finiteOcc.car.comp <- sum(z.car.comp[])

##collembola data
##priors
beta0.coll ~ dnorm(0, 0.01)
beta.Cutpartial.coll ~ dnorm(0, 0.01)
beta.Cutclear.coll ~ dnorm(0, 0.01)
beta.z.sal.coll ~ dnorm(0, 0.01)
beta.z.car.prey.coll ~ dnorm(0, 0.01)
beta.z.car.comp.coll ~ dnorm(0, 0.01)

##block random effect
for(m in 1:nblocks) {
   alpha.block.coll[m] ~ dnorm(0, tau.block.coll)
}

tau.block.coll <- pow(sigma.block.coll, -2)
sigma.block.coll ~ dunif(0, 50)

##allow each group to have different variance
for(j in 1:3) {
   tau.coll[j] <- pow(sigma.coll[j], -2)
   sigma.coll[j] ~ dunif(0, 150)
}

##iterate over each observation
for (i in 1:nsites) {

    ##linear predictor  
    mu.coll[i] <- beta0.coll +
    beta.Cutpartial.coll*Cutpartial[i] +
    beta.Cutclear.coll*Cutclear[i] + beta.z.sal.coll*z.sal[i] +
    beta.z.car.prey.coll*z.car.prey[i] +
    beta.z.car.comp.coll*z.car.comp[i] + alpha.block.coll[Block[i]]

    ##response
    y.coll[i] ~ dnorm(mu.coll[i], tau.coll[Group[i]])
}

##derived parameters
for(i in 1:nsites) {
    pred.coll[i] <- mu.coll[i]
    res.coll[i] <- y.coll[i] - mu.coll[i]
    res.pearson[i] <- res.coll[i]/sigma.coll[Group[i]]
}
}
\end{lstlisting}


